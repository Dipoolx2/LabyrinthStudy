plugins {
  id 'application'
  id 'org.openjfx.javafxplugin' version '0.0.13'
  id 'org.beryx.jlink' version '2.25.0'
}

group 'com.mazestudy'
version '1.1.1'

repositories {
  mavenCentral()
}

javafx {
  version = '19.0.2'
  modules = ['javafx.controls', 'javafx.fxml']
}

application {
  mainModule = 'game.labyrinthstudy'
  mainClass = 'game.labyrinthstudy.MainApplication'
  applicationDefaultJvmArgs = [
          '-Dprism.allowhidpi=false' // Forces software rendering
  ]
}

dependencies {
  // Add any additional dependencies here
}

tasks.withType(Copy) {
  duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

jlink {
  imageZip = project.file("${buildDir}/distributions/app-${javafx.platform.classifier}.zip")
  options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']

  forceMerge('log4j-api')

  launcher {
    name = 'MazeStudy'
    jvmArgs = ['-Dprism.order=sw']
  }

  jpackage {
    installerType = 'msi'
    installerOptions = [
            '--vendor', 'TSP - Group A3',
            '--description', 'Maze Study Software',
            '--win-dir-chooser',
            '--win-menu',
            '--win-shortcut',
            '--resource-dir', "${buildDir}/resources"
    ]
    skipInstaller = false
  }
}

// Ensure resources are in the jar
processResources {
  from('src/main/resources') {
    include '**/*'
  }
}

// Ensure resources are copied to the runtime image
tasks.jlink {
  doLast {
    copy {
      from("${buildDir}/resources/main") {
        eachFile { details ->
          // Remove the path prefix to flatten the structure
          details.path = details.name
        }
      }
      into("${buildDir}/image/bin/resources")
      includeEmptyDirs = false
    }
  }
}

